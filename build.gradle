buildscript {
    ext {
        cleanArchitectureVersion = '3.20.15'
        springBootVersion = '3.5.5'
        springCoreVersion = '6.2.11'
        sonarVersion = '6.0.1.5171'
        jacocoVersion = '0.8.12'
        pitestVersion = '1.15.0'
        lombokVersion = '1.18.38'
        junitVersion = '5.12.2'
        reactorVersion = '3.7.6'
        jacksonDatabindVersion = '2.19.0'
        log4jVersion = '2.24.3'
        commonslang3= '3.18.0'
        nettyVersion = '4.1.127.Final'
    }

}

plugins {
    id 'co.com.bancolombia.cleanArchitecture' version "${cleanArchitectureVersion}"
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'info.solidsoft.pitest' version "${pitestVersion}" apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'org.owasp.dependencycheck' version '12.1.8' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.sonarqube' version "${sonarVersion}"
    id 'maven-publish'
    id 'jacoco'
}

ext{
    set('netty.version', "${nettyVersion}")
}

sonar {
    def modules = subprojects.projectDir.collect { "${it.toString().replace(project.projectDir.toString() + "/", "")}" }
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.organization", "grupo-bancolombia"
        property "sonar.host.url", "https://sonarcloud.io/"
        property "sonar.modules", "${modules.join(',')}"
        property "sonar.sources", "src,settings.gradle,main.gradle,build.gradle,${modules.collect { "${it}/build.gradle" }.join(',')}"
        property "sonar.exclusions", "**/test/**, **/ReactiveLogsConfiguration.java, **/ImperativeLogsConfiguration.java, **/middleware/**, " +
                "**/config/**"
        property "sonar.test", "src/test"
        property "sonar.java.binaries", ""
        property "sonar.junit.reportsPath", "build/test-results/test"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacocoMergedReport/jacocoMergedReport.xml"
        property "sonar.pitest.reportPaths", "build/reports/pitest/mutations.xml"
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

apply from: './main.gradle'
