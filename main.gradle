apply plugin: 'info.solidsoft.pitest.aggregator'

allprojects {
    apply from: "${rootDir}/dependencyMgmt.gradle"
    apply plugin: 'java'
    apply plugin: 'org.owasp.dependencycheck'

    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    group 'com.github.bancolombia'
}

subprojects {
    apply plugin: 'co.com.bancolombia.cleanArchitecture'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'signing'

    ext {
        groupId = 'com.github.bancolombia'
        artifactId = project.name
        version = project.findProperty("version") ?: "0.0.0"

        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/mit-license.php"
                    distribution "repo"
                }
            }
            developers {
                developer {
                    id "avendanodanny"
                    name "Danny Farid AvendaÃ±o Ojeda"
                    email "dannyfuv@gmail.com"
                }
                developer {
                    id "RicharLeon"
                    name "Richar Daniel Diaz Leon"
                    email "rleon97@hotmail.com"
                }
                developer {
                    id "lectrapb"
                    name "Giovanny Alexander Porras Barbosa"
                    email "pbgiovanny@yahoo.com"
                }
            }

            scm {
                url "git@github.com:bancolombia/ecs-logs-java.git"
            }
        }
        groupId = 'com.github.bancolombia'
    }

    dependencies {
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }

    pitest {
        targetClasses = ['co.com.bancolombia.*']
        excludedClasses = [
                'co.com.bancolombia.ecs.domain.middleware.*',
                'co.com.bancolombia.ecs.infra.config.*'
        ]
        excludedTestClasses = [
                'co.com.bancolombia.ecs.domain.middleware.*',
                'co.com.bancolombia.ecs.infra.config.*'
        ]
        pitestVersion = '1.17.1'
        verbose = false
        outputFormats = ['XML', 'HTML']
        threads = 8
        exportLineCoverage = true
        useClasspathFile = true
        timestampedReports = false
        fileExtensionsToFilter.addAll('xml', 'orbit')
        junit5PluginVersion = '1.2.1'
        failWhenNoMutations = false
        jvmArgs = ["-XX:+AllowRedefinitionToAddDeleteMethods"]
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)


    jacocoTestReport {
        dependsOn = [test, 'pitest']
        reports {
            xml.setRequired true
            xml.setOutputLocation layout.buildDirectory.file("reports/jacoco.xml")
            csv.setRequired false
            html.setOutputLocation layout.buildDirectory.dir("reports/jacocoHtml")
        }
    }

    //    check.dependsOn dependencyCheckAggregate

    dependencyCheck {
        format = 'JSON'
        formats = ['JSON']
    }

    java {
        withJavadocJar()
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
        failOnError = false
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    //Publishing configuration
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                groupId = groupId
                artifactId = project.name
                version =  project.findProperty("version") ?: "0.0.0"

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', artifactDescription)
                    root.appendNode('name', project.name)
                    root.appendNode('url', 'https://github.com/bancolombia/ecs-logs-java')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    def signingKeyId = System.getenv("SIGNING_KEY_ID") ?: project.findProperty("signing.keyId")
    def signingKey = System.getenv("SIGNING_KEY_FILE") ?: project.findProperty("signing.key")
    def signingPassword = System.getenv("SIGNING_PASSWORD") ?: project.findProperty("signing.password")

    if (signingKeyId && signingKey && signingPassword) {
        signing {
            useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
            sign publishing.publications.mavenJava
        }
    } else {
        logger.lifecycle("Firma omitida: No se encontraron las variables de entorno o propiedades necesarias.")
    }
}

jacoco {
    toolVersion = jacocoVersion
    reportsDirectory.set(layout.buildDirectory.dir("reports"))
}

tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn = [
            test,
            subprojects.test,
            subprojects.javadoc,
            subprojects.jacocoTestReport,
            pitestReportAggregate
    ].flatten()
    additionalSourceDirs.setFrom files(subprojects*.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects*.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects*.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-Amapstruct.suppressGeneratorTimestamp=true']
}

pitestReportAggregate {
    doLast {
        def reportDir = layout.buildDirectory.dir("reports/pitest").get().asFile
        def consolidatedReport = new File(reportDir, 'mutations.xml')
        consolidatedReport.withWriter { writer ->
            writer.write("<mutations>\n")
            subprojects.each { subproject ->
                def xmlReport = subproject.layout.buildDirectory.file("reports/pitest/mutations.xml").get().asFile
                if (xmlReport.exists()) {
                    def xmlContent = xmlReport.text
                    xmlContent = xmlContent.replaceAll("<\\?xml[^>]*>", "")
                    xmlContent = xmlContent.replaceAll("</?mutations( partial=\"true\")?>", "")
                    writer.write(xmlContent.trim() + "\n")
                }
            }
            writer.write("</mutations>")
        }
    }
}

tasks.named('wrapper') {
    gradleVersion = '8.13'
}
